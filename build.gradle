buildscript {

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath GradlePlugins.kotlin

        classpath GradlePlugins.build_config

        classpath GradlePlugins.shadow_jar
        classpath GradlePlugins.versions_plugin
    }
}

apply plugin: 'kotlin'
apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'idea' // Required for build config IDE support
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
    maven { url "http://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    implementation Deps.kotlin_stdlib_jdk8
    implementation Deps.kotlin_reflection  // can be removed later

    implementation project(":glow-common")
    implementation project(":glow-cli")

    implementation Deps.kotlinx_html

    implementation Deps.slf4j_simple

    implementation Deps.arrow_core_data
    implementation Deps.arrow_core_extensions

    implementation Deps.koin

    implementation Deps.clikt
    implementation Deps.jcommander
    implementation Deps.json

    implementation Deps.javalin

    implementation Deps.flexmark
    implementation Deps.jmustache
    implementation Deps.eo_yaml

    testImplementation Deps.kotlin_test
}

mainClassName = 'io.github.dector.glow.GlowKt'

shadowJar {
    classifier = null
}

task distrib(dependsOn: 'shadowJar') {
    doLast {
        def distribFile = file("./distrib/${jar.archiveName}")

        copy {
            from jar.archivePath
            into distribFile.parentFile
        }

        def relativePath = file('.').toURI().relativize(distribFile.toURI())
        println "Distribution saved to ./$relativePath"
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [
                "-XXLanguage:+InlineClasses",
                "-XXLanguage:+NewInference",
                "-Xallow-result-return-type"
        ]
    }
}

allprojects {
    group 'io.github.dector.glow'
    version = Config.version

    repositories {
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    test {
        useJUnitPlatform()
    }
}
